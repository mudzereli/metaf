~~ {
~~ FOR AUTO-COMPLETION ASSISTANCE: testvar getvar setvar touchvar clearallvars clearvar getcharintprop getchardoubleprop getcharquadprop getcharboolprop getcharstringprop getisspellknown getcancastspell_hunt getcancastspell_buff getcharvital_base getcharvital_current getcharvital_buffedmax getcharskill_traininglevel getcharskill_base getcharskill_buffed getplayerlandcell getplayercoordinates coordinategetns coordinategetwe coordinategetz coordinatetostring coordinateparse coordinatedistancewithz coordinatedistanceflat wobjectgetphysicscoordinates wobjectgetname wobjectgetobjectclass wobjectgettemplatetype wobjectgetisdooropen wobjectfindnearestmonster wobjectfindnearestdoor wobjectfindnearestbyobjectclass wobjectfindininventorybytemplatetype wobjectfindininventorybyname wobjectfindininventorybynamerx wobjectgetselection wobjectgetplayer wobjectfindnearestbynameandobjectclass actiontryselect actiontryuseitem actiontryapplyitem actiontrygiveitem actiontryequipanywand actiontrycastbyid actiontrycastbyidontarget chatbox chatboxpaste statushud statushudcolored uigetcontrol uisetlabel isfalse istrue iif randint cstr strlen getobjectinternaltype cstrf stopwatchcreate stopwatchstart stopwatchstop stopwatchelapsedseconds cnumber floor ceiling round abs getworldname getitemcountininventorybyname getheading getitemcountininventorybynamerx getheadingto actiontrygiveprofile vitae getfellowshipstatus getfellowshipname getfellowshipisopen getfellowshipisleader getfellowshipleaderid getfellowshipcanrecruit getfellowid getfellowshipcount getfellowshiplocked getfellowname getfellowshipisfull sin cos tan sqrt asin acos atan atan2 sinh cosh tanh vtsetmetastate getregexmatch echo chr ord wobjectgetid wobjectgethealth wobjectfindbyid wobjectgetintprop wobjectfindnearestbytemplatetype wobjectgetopencontainer testquestflag getquestktprogress isrefreshingquests getquestktrequired getqueststatus getisday getgamehour getgamehourname getisnight getgameday getgameticks getminutesuntilday getgamemonth getgamemonthname getminutesuntilnight getgameyear uisetvisible uiviewvisible uiviewexists getgvar touchgvar getpvar touchpvar setgvar cleargvar setpvar clearpvar testgvar clearallgvars testpvar clearallpvars dictgetitem dictcreate dicthaskey dictadditem dictkeys dictremovekey dictvalues dictclear dictsize dictcopy listgetitem listpop listcreate listcontains listremove listadd listindexof listremoveat listinsert listlastindexof listclear listcopy listcount listreverse

~~																						
~~ File auto-generated by metaf, a program created by Eskarina of Morningthaw/Coldeve.	
~~		Get metaf here: https://github.com/JJEII/metaf/									
~~																						
~~ All recognized structural designators:												
~~		STATE:				DO:															
~~		IF:					NAV:														
~~																						
~~ All recognized CONDITION (IF:) operation keywords:									
~~		Never				NavEmpty			MobsInDist_Priority		Not				
~~		Always				Death				NeedToBuff				PSecsInStateGE	
~~		All					VendorOpen			NoMobsInDist			SecsOnSpellGE	
~~		Any					VendorClosed		BlockE					BuPercentGE		
~~		ChatMatch			ItemCountLE			CellE					DistToRteGE		
~~		MainSlotsLE			ItemCountGE			IntoPortal				Expr			
~~		SecsInStateGE		MobsInDist_Name		ExitPortal				ChatCapture		
~~																						
~~ All recognized ACTION (DO:) operation keywords:										
~~		None				EmbedNav			ChatExpr				SetOpt			
~~		SetState			CallState			SetWatchdog				CreateView		
~~		Chat				Return				ClearWatchdog			DestroyView		
~~		DoAll				DoExpr				GetOpt					DestroyAllViews	
~~																						
~~ All recognized NAV types:															
~~		circular			follow														
~~		linear				once														
~~																						
~~ All recognized NAV NODE types:														
~~		flw					vnd															
~~		pnt					ptl															
~~		rcl					tlk															
~~		pau					chk															
~~		cht					jmp															
~~		prt (deprecated in VTank)														
~~ }																					
STATE: {Default} ~~{
	IF:	Always
		DO:	SetState {*SetupOptions*}
~~}
STATE: {*SetupOptions*} ~~{
	IF:	Always
		DO:	DoExpr {setvar[ncs-cmd-load-window-profile,`/vwt apply def`]}
	IF:	Always
		DO:	DoAll
				ChatExpr {getvar[ncs-cmd-load-window-profile]}
				SetOpt {OpenDoors} {true}
				SetOpt {EnableNav} {true}
				SetOpt {EnableBuffing} {false}
				SetOpt {Recharge-Norm-Mana} {0}
				SetOpt {NavCloseStopRange} {0.00833}
				Chat {/ub opt set VTank.PatchExpressionEngine true}
				Chat {/mt click OK}
				DoExpr {echo[`[META]: Starting New Character Setup`,3]}
				SetState {GoToJonathan}
~~}
STATE: {GoToJonathan} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Jonathan`,3]}
	IF: Always
		DO:	EmbedNav navGoToJonathan {GoToJonathan}
	IF:	NavEmpty
		DO:	SetState {TalkToJonathan}	
~~}
STATE: {TalkToJonathan} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Leaving the Academy`,3]}
	IF:	IntoPortal
		DO:	SetState {TalkToAlcott}
	IF:	ItemCountGE 1 {Academy Exit Token}
		DO:	Chat {/ub give Academy Exit Token to Jonathan}
	IF: Always
		DO:	DoAll
				EmbedNav navTalkToJonathan {TalkToJonathan}
				SetWatchdog 3 5 {TalkToJonathan}
~~}
STATE: {TalkToAlcott} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Talking to Alcott`,3]}
	IF:	ChatMatch {^Alcott tells you}
		DO:	SetState {GoToPathwarden}
	IF: ExitPortal
		DO:	DoAll
				Chat {/mt click OK}
				SetOpt {OpenDoors} {false}
	IF:	Always
		DO:	EmbedNav navTalkToAlcott {TalkToAlcott}
~~}
STATE: {GoToPathwarden} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Pathwarden Thorolf`,3]}
	IF: Always
		DO: EmbedNav navGoToPathwarden {GoToPathwarden}
	IF:	All
			NavEmpty
			ItemCountGE 1 {Pathwarden Token}
		DO:	Chat {/ub give Pathwarden Token to Pathwarden Thorolf}
	IF:	ItemCountGE 1 {Pathwarden Supply Key}
		DO: SetState {GoToPathwardenChest}
~~}
STATE: {GoToPathwardenChest} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Moving to the Aluvian Pathwarden Chest`,3]}
	IF:	Always
		DO:	EmbedNav navGoToPathwardenChest {GoToPathwardenChest}
	IF:	NavEmpty
		DO:	SetState {OpenPathwardenChest}
~~}
STATE: {OpenPathwardenChest} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Opening and Looting the Aluvian Pathwarden Chest`,3]}
	IF:	ItemCountGE 1 {Pathwarden Trinket}
		DO:	DoAll
				Chat {/mt click OK}
				Chat {/ub use Pathwarden Trinket}
				SetState {GoToTownNetwork}
	IF:	Expr {getobjectinternaltype[wobjectgetopencontainer[]]==0||wobjectgetopencontainer[]==0}
		DO:	DoAll
				Chat {/ub use Pathwarden Supply Key on Aluvian Pathwarden Chest}
				DoExpr {actiontryuseitem[wobjectfindnearestbynameandobjectclass[10,`Aluvian Pathwarden Chest`]]}
				SetWatchdog 3 3 {OpenPathwardenChest}
	IF:	Expr {wobjectgetopencontainer[]&&1}
		DO:	DoAll
				Chat {/ub use Pathwarden Trinket}
				SetWatchdog 3 5 {OpenPathwardenChest}
~~}
STATE: {GoToTownNetwork} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Town Network`,3]}
	IF:	CellE 00070143
		DO:	SetState {GoToArwic}
	IF:	Always
		DO:	EmbedNav navGoToTownNetwork {GoToTownNetwork}
~~}
STATE: {GoToArwic} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Arwic`,3]}
	IF:	CellE C6A90009
		DO:	SetState {GoToArwicVendor}
	IF:	Always
		DO:	EmbedNav navGoToArwic {GoToArwic}
~~}
STATE: {GoToArwicVendor} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Arwic Vendor`,3]}
	IF:	Always
		DO:	EmbedNav navGoToArwicVendor {GoToArwicVendor}
	IF:	NavEmpty
		DO:	SetState {BuyPacksFromVendor}
~~}
STATE: {BuyPacksFromVendor} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Buying Packs From Vendor`,3]}
	IF:	ItemCountGE 6 {Pack}
		DO:	SetState {GoToArwicLifestone}
	IF:	Always
		DO:	DoAll
				EmbedNav navBuyPacksFromVendor {BuyPacksFromVendor}
				SetWatchdog 3 10 {BuyPacksFromVendor}
~~}
STATE: {GoToArwicLifestone} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Going to Arwic Lifestone`,3]}
	IF:	Always
		DO:	EmbedNav navGoToArwicLifestone {GoToArwicLifestone}
	IF:	NavEmpty
		DO:	SetState {AttuneToLifestone}
~~}
STATE: {AttuneToLifestone} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Attuning to Arwic Lifestone`,3]}
	IF:	ChatMatch {^You have attuned your spirit to this Lifestone}
		DO:	SetState {TalkToMonroe}
	IF:	Always
		DO:	Chat {/ub usel Life Stone}
~~}
STATE: {TalkToMonroe} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Talking to Monroe`,3]}
	IF:	Always
		DO:	EmbedNav navTalkToMonroe {TalkToMonroe}
	IF:	NavEmpty
		DO:	SetState {SortBags}
~~}
STATE: {SortBags} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: Sorting Bags`,3]}
	IF:	Always
		DO:	EmbedNav navSortBags {SortBags}
	IF: ChatMatch {^You think\, \"done sorting\!\"$}
		DO:	SetState {AllDone}
~~}
STATE: {AllDone} ~~{
	IF:	Always
		DO:	DoExpr {echo[`[META]: New Character Setup Complete!`,3]}
~~}

NAV: navGoToJonathan once ~~ {
	pnt 5.30051158666611 -100.468752280871 2.08333134651184E-05
	pnt 5.30884963671366 -100.472736517588 2.08333134651184E-05
	pnt 5.32123756408691 -100.475580787659 2.08333134651184E-05
	pnt 5.32994709014893 -100.472106480598 2.08333134651184E-05
	pnt 5.33200130462646 -100.458431887627 2.08333134651184E-05
	pnt 5.33872651259104 -100.437575721741 2.08333134651184E-05
~~ }
NAV: navTalkToJonathan once ~~ {
	tlk 5.33872651259104 -100.437575721741 2.08333134651184E-05 5.34214124679565 -100.42975833416 2.08333131013205E-05 37 {Jonathan}
~~ }
NAV: navTalkToAlcott once ~~ {
	pnt 33.6 42.079583332936 0.391687520345052
	pnt 33.600684261322 42.1056834300359 0.391687520345052
	tlk 33.600684261322 42.1056834300359 0.391687520345052 33.5970837593079 42.1084854165713 0.391687512397766 37 {Alcott}
~~ }
NAV: navGoToPathwarden once ~~ {
	pnt 33.6011482874552 42.1017341335615 0.391687520345052
	pnt 33.6122534751892 42.1195231040319 0.391687520345052
	pnt 33.6308286348979 42.1435496171315 0.391687520345052
	pnt 33.6497922579447 42.1610944986343 0.391687520345052
~~ }
NAV: navGoToPathwardenChest once ~~ {
	pnt 33.6478043556213 42.1582038164139 0.391687520345052
	pnt 33.6602661768595 42.1550102392832 0.391687520345052
	pnt 33.6629225413005 42.1683336575826 0.391687520345052
~~ }
NAV: navGoToTownNetwork once ~~ {
	pnt 33.6618388175964 42.1650496562322 0.391687520345052
	pnt 33.6397918701172 42.1427082538605 0.391687520345052
	pnt 33.6149850527446 42.1448077440262 0.391687520345052
	pnt 33.5647213617961 42.1502336661021 0.391687520345052
	pnt 33.5363924662272 42.1592285871506 0.383710543314616
	pnt 33.5073656717936 42.1718861818314 0.368451658884684
	pnt 33.4807059605916 42.1864555199941 0.353701114654541
	pnt 33.455286582311 42.2053403218587 0.338388506571452
	pnt 33.432988913854 42.221905930837 0.332044347127279
	pnt 33.4044997374217 42.2341622034709 0.328980286916097
	pnt 33.3768640120824 42.2470861593882 0.3257493019104
	pnt 33.3542811075846 42.2590483188629 0.325020853678385
	pnt 33.3312498251597 42.2712479750315 0.325020853678385
	ptl 33.331000606219 42.2720585982005 0.325020853678385 33.3099691669146 42.2817183335622 0.32582500576973 14 {Portal to Town Network}
~~ }
NAV: navGoToArwic once ~~ {
	pnt -101.658333333333 -96.6 2.08333134651184E-05
	pnt -101.660239092509 -96.5773980299632 2.08333134651184E-05
	pnt -101.660252094269 -96.5436454137166 2.08333134651184E-05
	pnt -101.652875995636 -96.5263944943746 2.08333134651184E-05
	pnt -101.64226586024 -96.5251459916433 2.08333134651184E-05
	pnt -101.611659908295 -96.518384885788 2.08333134651184E-05
	pnt -101.601229127248 -96.5160760084788 2.08333134651184E-05
	ptl -101.601229127248 -96.5160760084788 2.08333134651184E-05 -101.588099161784 -96.5166525046031 -0.000262499845121056 14 {Portal to Arwic}
~~ }
NAV: navGoToArwicVendor once ~~ {
	pnt 56.6450208346049 33.2752167165279 0.175020837783813
	pnt 56.6478301048279 33.3284719705582 0.175020837783813
	pnt 56.6480444749196 33.3690061569214 0.175020837783813
~~ }
NAV: navGoToArwicLifestone once ~~ {
	pnt 56.6455473899841 33.3976800759633 0.175020837783813
	pnt 56.6463928699493 33.4369568824768 0.175020837783813
	pnt 56.6474084059397 33.463033692042 0.175020837783813
	pnt 56.6487401167552 33.4891107877096 0.175020837783813
	pnt 56.6500434875488 33.5146330515544 0.175020837783813
	pnt 56.6585380395253 33.5351223309835 0.175020837783813
	pnt 56.6827981154124 33.5445864677429 0.175020837783813
	pnt 56.7068646589915 33.5531822522481 0.175020837783813
	pnt 56.738071791331 33.5661099433899 0.175020837783813
	pnt 56.766908677419 33.5831163088481 0.175020837783813
~~ }
NAV: navTalkToMonroe once ~~ {
	pnt 56.7814922650655 33.6411301930745 0.175027084350586
	tlk 56.7814922650655 33.6411301930745 0.175027084350586 56.7816291809082 33.6455291748047 0.175020843744278 37 {Monroe}
~~ }
NAV: navBuyPacksFromVendor once ~~{
	cht 0 0 0 {/ub vendor open Shopkeep Mirinda}
	pau 0 0 0 3000
	cht 0 0 0 {/ub vendor addbuy 6 Pack}
	pau 0 0 0 300
	cht 0 0 0 {/ub vendor buyall}
	pau 0 0 0 300
~~}
NAV: navSortBags once ~~{
	cht 0 0 0 {/ms set source player}
	pau 0 0 0 200
	cht 0 0 0 {/ms set dest pack 7}
	pau 0 0 0 200
	cht 0 0 0 {/ms set flags OC}
	pau 0 0 0 200
	cht 0 0 0 {/ms start}
~~}