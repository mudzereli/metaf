~~ {
~~ FOR AUTO-COMPLETION ASSISTANCE: testvar getvar setvar touchvar clearallvars clearvar getcharintprop getchardoubleprop getcharquadprop getcharboolprop getcharstringprop getisspellknown getcancastspell_hunt getcancastspell_buff getcharvital_base getcharvital_current getcharvital_buffedmax getcharskill_traininglevel getcharskill_base getcharskill_buffed getplayerlandcell getplayercoordinates coordinategetns coordinategetwe coordinategetz coordinatetostring coordinateparse coordinatedistancewithz coordinatedistanceflat wobjectgetphysicscoordinates wobjectgetname wobjectgetobjectclass wobjectgettemplatetype wobjectgetisdooropen wobjectfindnearestmonster wobjectfindnearestdoor wobjectfindnearestbyobjectclass wobjectfindininventorybytemplatetype wobjectfindininventorybyname wobjectfindininventorybynamerx wobjectgetselection wobjectgetplayer wobjectfindnearestbynameandobjectclass actiontryselect actiontryuseitem actiontryapplyitem actiontrygiveitem actiontryequipanywand actiontrycastbyid actiontrycastbyidontarget chatbox chatboxpaste statushud statushudcolored uigetcontrol uisetlabel isfalse istrue iif randint cstr strlen getobjectinternaltype cstrf stopwatchcreate stopwatchstart stopwatchstop stopwatchelapsedseconds cnumber floor ceiling round abs getworldname getitemcountininventorybyname getheading getitemcountininventorybynamerx getheadingto actiontrygiveprofile vitae getfellowshipstatus getfellowshipname getfellowshipisopen getfellowshipisleader getfellowshipleaderid getfellowshipcanrecruit getfellowid getfellowshipcount getfellowshiplocked getfellowname getfellowshipisfull sin cos tan sqrt asin acos atan atan2 sinh cosh tanh vtsetmetastate getregexmatch echo chr ord wobjectgetid wobjectgethealth wobjectfindbyid wobjectgetintprop wobjectfindnearestbytemplatetype wobjectgetopencontainer testquestflag getquestktprogress isrefreshingquests getquestktrequired getqueststatus getisday getgamehour getgamehourname getisnight getgameday getgameticks getminutesuntilday getgamemonth getgamemonthname getminutesuntilnight getgameyear uisetvisible uiviewvisible uiviewexists getgvar touchgvar getpvar touchpvar setgvar cleargvar setpvar clearpvar testgvar clearallgvars testpvar clearallpvars dictgetitem dictcreate dicthaskey dictadditem dictkeys dictremovekey dictvalues dictclear dictsize dictcopy listgetitem listpop listcreate listcontains listremove listadd listindexof listremoveat listinsert listlastindexof listclear listcopy listcount listreverse

~~																						
~~ File auto-generated by metaf, a program created by Eskarina of Morningthaw/Coldeve.	
~~		Get metaf here: https://github.com/JJEII/metaf/									
~~																						
~~ All recognized structural designators:												
~~		STATE:				DO:															
~~		IF:					NAV:														
~~																						
~~ All recognized CONDITION (IF:) operation keywords:									
~~		Never				NavEmpty			MobsInDist_Priority		Not				
~~		Always				Death				NeedToBuff				PSecsInStateGE	
~~		All					VendorOpen			NoMobsInDist			SecsOnSpellGE	
~~		Any					VendorClosed		BlockE					BuPercentGE		
~~		ChatMatch			ItemCountLE			CellE					DistToRteGE		
~~		MainSlotsLE			ItemCountGE			IntoPortal				Expr			
~~		SecsInStateGE		MobsInDist_Name		ExitPortal				ChatCapture		
~~																						
~~ All recognized ACTION (DO:) operation keywords:										
~~		None				EmbedNav			ChatExpr				SetOpt			
~~		SetState			CallState			SetWatchdog				CreateView		
~~		Chat				Return				ClearWatchdog			DestroyView		
~~		DoAll				DoExpr				GetOpt					DestroyAllViews	
~~																						
~~ All recognized NAV types:															
~~		circular			follow														
~~		linear				once														
~~																						
~~ All recognized NAV NODE types:														
~~		flw					vnd															
~~		pnt					ptl															
~~		rcl					tlk															
~~		pau					chk															
~~		cht					jmp															
~~		prt (deprecated in VTank)														
~~ }

STATE: {Default} ~~ {
	IF:	Always
		DO:	SetState {*SetupOptions*}
~~ }
STATE: {*SetupOptions*} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Setting up for Colosseum`,19]}
	IF:	Always
		DO:	DoAll
				EmbedNav navNone {None}
				SetOpt {LootOnlyRareCorpses} {true}
				SetOpt {LootPriorityBoost} {true}
				SetOpt {EnableBuffing} {false}
				SetOpt {AttackDistance} {0.13333}
				SetOpt {ApproachDistance} {0.13333}
				SetOpt {NavCloseStopRange} {0.021}
				SetOpt {EnableCombat} {false}
				SetOpt {PetRangeMode} {0}
				SetOpt {EnableNav} {true}
				SetOpt {LootFellowCorpses} {false}
				SetOpt {CastDispelSelf} {false}
				DoExpr {setgvar[qcolo-looping,0]}
				DoExpr {setgvar[qcolo-chars,listcreate[]]}
				DoExpr {setgvar[qcolo-charswaiting,listcreate[]]}
				DoExpr {setgvar[qcolo-charsbuffed,listcreate[]]}
				DoExpr {setgvar[qcolo-charsreadyforturnin,listcreate[]]}
				DoExpr {setgvar[qcolo-fellowlocked,0]}
				DoExpr {setgvar[qcolo-allowsolo,1]}
				DoExpr {setgvar[qcolo-enablebuffing,0]}
				DoExpr {setgvar[qcolo-adminmode,0]}
	IF:	SecsInStateGE 8
		DO:	SetState {SynchronizeChars}
~~ }
STATE: {SynchronizeChars} ~~ {
	IF:	All
			Expr {getfellowshipcount[]>=3||getgvar[qcolo-allowsolo]==1}
			Not Expr {listcontains[getgvar[qcolo-chars],getcharstringprop[1]]}
		DO:	DoAll
				DoExpr {echo[`[META]: Synchronizing Colosseum Meta on all Characters`,19]}
				DoExpr {listadd[getgvar[qcolo-chars],getcharstringprop[1]]}
	IF:	All
			SecsInStateGE 2
			Expr {getfellowshipcount[]>=3||getgvar[qcolo-allowsolo]==1}
			Not Expr {listcontains[getgvar[qcolo-chars],getcharstringprop[1]]}
		DO:	SetState {SynchronizeChars}
	IF:	All
			SecsInStateGE 8
			Expr {getfellowshipcount[]>=3||getgvar[qcolo-allowsolo]==1}
		DO:	DoAll
				DoExpr {echo[`[META]: Starting Colosseum with `+listcount[getgvar[qcolo-chars]]+` Characters!`,19]}
				SetState {ProgressCheck}
~~ }
STATE: {ProgressCheck} ~~ {
	IF:	Expr {wobjectfindnearestbynameandobjectclass[37,Arena Bell] && coordinatedistancewithz[getplayercoordinates[], wobjectgetphysicscoordinates[wobjectfindnearestbynameandobjectclass[37,Arena Bell]]] <= 25}
		DO:	SetState {StartCurrentRoom}
	IF:	BlockE 00AF0000
		DO:	SetState {SynchronizeWaitingForStart}
	IF:	Always
		DO:	SetState {GoToColo}
~~ }
STATE: {GoToColo} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Recalling to Colosseum`,19]}
	~~ we're here so stop casting
	IF:	BlockE 00AF0000
		DO:	SetState {ProgressCheck}
	~~ just keep trying to spam the spell over and over again
	~~ Colosseum Recall Spell ID = 4213
	IF:	Expr {actiontrycastbyid[4213]}
		DO: SetState {GoToColo}
~~ }
STATE: {SynchronizeWaitingForStart} ~~ {
	IF: Always
		DO:	DoExpr {echo[`[META]: Synchronizing all Characters @ Waiting For Start`,19]}
	IF:	Not Expr {listcontains[getgvar[qcolo-charswaiting],getcharstringprop[1]]}
		DO:	DoExpr {listadd[getgvar[qcolo-charswaiting],getcharstringprop[1]]}
	IF:	All
			SecsInStateGE 2
			Not Expr {listcontains[getgvar[qcolo-charswaiting],getcharstringprop[1]]}
		DO:	SetState {SynchronizeWaitingForStart}
	IF: Expr {listcount[getgvar[qcolo-charswaiting]]>=listcount[getgvar[qcolo-chars]]}
		DO:	DoAll
				DoExpr {echo[`[META]: Synchronized `+listcount[getgvar[qcolo-charswaiting]]+` Characters @ Waiting For Start`,19]}
				SetState {WaitingForStart}
~~ }
STATE: {WaitingForStart} ~~ {
	IF:	Always
		DO:	DoAll
				DoExpr {echo[`[META]: Waiting For Start`,19]}
				DoExpr {echo[`[META]: Following commands are available:`,19]}
				DoExpr {echo[`[META]: !start - start the colo run`,19]}
				DoExpr {echo[`[META]: !startl - start the colo run, but loop & keep going after finishing`,19]}
				DoExpr {echo[`[META]: !tickets - buy colo tickets`,19]}
				DoExpr {echo[`[META]: !rings - use keys & loot colo rings`,19]}
	~~ this sets up all the initial variables when waiting to start
	IF:	Always
		DO:	DoAll
				DoExpr {setgvar[qcolo-charsreadyforturnin,listcreate[]]}
				SetOpt {EnableCombat} {false}
				SetOpt {EnableLooting} {false}
				EmbedNav navTalkMasterArbitrator {TalkMasterArbitrator}
	~~ this condition puts you back into the arena if you reload the script after leaving
	IF:	ChatMatch {Master Arbitrator tells you\, \"Your fellowship\'s Arena battles still continue. }
		DO:	DoAll
				DoExpr {setvar[qcolo-started,1]}
				SetState {EnterFirstRoom}
	~~ this condition will automatically start the next run if qcolo-looping is set to 1
	IF:	All
			ItemCountGE 1 {Colosseum Ticket}
			Expr {getgvar[qcolo-looping]==1}
			Expr {listcount[getgvar[qcolo-charswaiting]]>=listcount[getgvar[qcolo-chars]]}
			SecsInStateGE 90
		DO:	SetState {GettingATicket}
	~~ this condition triggers when the ticket holder receives a ticket stub
	IF:	Any
			Expr {getgvar[qcolo-fellowlocked]==1}
			ChatMatch {\[Fellowship\].* says\, \"Received a Colosseum Ticket Stub\. Enter the first room and start buffing\!"}
		DO:	SetState {EnterFirstRoom}
	~~ this condition is used to manually start the script [!startl in fellow] (with looping)
	IF:	ChatMatch {\[Fellowship\] You say\, \"\!startl\"}
		DO:	DoAll
				DoExpr {setgvar[qcolo-looping,1]}
				SetState {GettingATicket}
	~~ this condition is used to manually start the script [!start in fellow] (without looping)
	IF:	ChatMatch {\[Fellowship\] You say\, \"\!start\"}
		DO:	SetState {GettingATicket}
	~~ this condition is used to start buying tickets
	IF:	ChatMatch {\[Fellowship\] You say\, \"\!tickets\"}
		DO:	SetState {BuyTickets}
	~~ this condition is used to start turning in rings
	IF:	ChatMatch {\[Fellowship\] .* say(s*)\, \"\!rings\"}
		DO:	SetState {LootRings}
~~ }
STATE: {GettingATicket} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Getting a Ticket for Entry`,19]}
	~~ if we get a ticket stub, start buffing and notify clients that it was received
	IF:	ItemCountGE 1 {Colosseum Ticket Stub}
		DO:	DoAll
				DoExpr {setgvar[qcolo-fellowlocked,1]}
				Chat {/f Received a Colosseum Ticket Stub. Enter the first room and start buffing!}
				SetState {EnterFirstRoom}
	~~ if we have a ticket but no stub, try to get a ticket
	IF:	All
			ItemCountGE 1 {Colosseum Ticket}
			Not ItemCountGE 1 {Colosseum Ticket Stub}
		DO:	Chat {/ub give Colosseum Ticket to Master Arbitrator}
	~~ if we're out of tickets, we're all done
	IF:	All
			ItemCountLE 0 {Colosseum Ticket Stub}
			ItemCountLE 0 {Colosseum Ticket}
		DO:	SetState {AllDone}
	~~ keep trying to get a ticket until an arena frees up
	IF:	All
			ItemCountLE 0 {Colosseum Ticket Stub}
			Any
				SecsInStateGE 10
				ChatMatch {^Master Arbitrator says\, \"Arena .* is now available for new warriors\!\"}
		DO:	SetState {GettingATicket}
~~ }
STATE: {EnterFirstRoom} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Entering First Room of the Colosseum`,19]}
	~~ first lets go into the arena, so that we can start our timer
	IF:	NavEmpty
		DO: DoAll
				DoExpr {setvar[qcolo-started,1]}
				Chat {/ub usel Advanced Colosseum Arena}
				SetWatchdog 3 3 {EnterFirstRoom}
	~~ ok we zoned into the arena, and we're actually going to start buffing
	IF:	Any
			IntoPortal
			ExitPortal
		DO:	SetState {BuffingBeforeRun}
~~ }
STATE: {BuffingBeforeRun} ~~ {
	IF: Expr {getgvar[qcolo-enablebuffing]==0}
		DO: DoAll
				SetOpt {EnableBuffing} {false}
				SetState {SynchronizeAfterBuffing}
	IF:	Always
		DO:	DoExpr {echo[`[META]: Buffing before Colosseum Run`,19]}
	~~ first lets go into the arena, so that we can start our timer
	IF:	Always
		DO:	DoAll
				Chat {/vt forcebuff}
				SetOpt {EnableBuffing} {true}
	~~ if we've been buffing for 3 minutes, or we finished by casting Blackmoor's Favor, then we're good to go.. probably
	IF:	Any
			SecsInStateGE 180
			ChatMatch {^You cast Blackmoor.*s Favor on yourself}
		DO:	SetState {SynchronizeAfterBuffing}
~~ }
STATE: {SynchronizeAfterBuffing} ~~ {
	IF: Always
		DO:	DoExpr {echo[`[META]: Synchronizing all Characters @ Finished Buffing`,19]}
	IF:	Not Expr {listcontains[getgvar[qcolo-charsbuffed],getcharstringprop[1]]}
		DO:	DoExpr {listadd[getgvar[qcolo-charsbuffed],getcharstringprop[1]]}
	IF:	All
			SecsInStateGE 2
			Not Expr {listcontains[getgvar[qcolo-charsbuffed],getcharstringprop[1]]}
		DO:	SetState {SynchronizeAfterBuffing}
	IF: Expr {listcount[getgvar[qcolo-charsbuffed]]>=listcount[getgvar[qcolo-chars]]}
		DO:	DoAll
				DoExpr {echo[`[META]: Synchronized `+listcount[getgvar[qcolo-charsbuffed]]+` Characters @ Finished Buffing`,19]}
				SetState {StartFirstRoom}
~~ }
STATE: {StartFirstRoom} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Starting the First Room of Colosseum`,19]}
	~~ do this special setup for starting the first room
	IF:	Always
		DO:	DoAll
				DoExpr {setvar[qcolo-roomnumber,5]}
				Chat {/ub myquests}
				SetState {StartCurrentRoom}
~~ }
STATE: {StartCurrentRoom} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Setting up for Colosseum Room: `+cstr[getvar[qcolo-roomnumber]+1],19]}
	~~ if we try to start the room, but we finished the run, then wait for our timer
	IF:	Any
			CellE 00AF0118
			CellE 00AF0117
		DO:	DoAll
				SetState {WaitingForRewards}
	~~ ring the bell, start killing & looting
	IF:	Always
		DO:	DoAll
				DoExpr {setvar[qcolo-roomnumber,getvar[qcolo-roomnumber]+1]}
				SetOpt {EnableCombat} {true}
				SetOpt {EnableLooting} {true}
				Chat {/ub usel Arena Bell}
				SetState {KillingCurrentRoom}
~~ }
STATE: {KillingCurrentRoom} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Starting Battle in Colosseum Room: `+getvar[qcolo-roomnumber],19]}
	IF: Death
		DO: DoAll
				DoExpr {echo[`[META]: Died in Colosseum Room: `+getvar[qcolo-roomnumber],19]}
				SetState {ProgressCheck}
	IF: All
			SecsInStateGE 60
			Expr {getgvar[qcolo-adminmode]==1}
		DO:	DoAll
				Chat {/objsend}
	IF: All
			SecsInStateGE 120
			Expr {getgvar[qcolo-adminmode]==1}
		DO:	DoAll
				Chat {/smite all}
				SetState {KillingCurrentRoom}
	IF:	Any
			ChatMatch {A Door appears on the North wall.}
			ChatMatch {Congratulations! You have beaten the Colosseum!}
			Expr {wobjectfindnearestbyobjectclass[14]&&coordinatedistancewithz[getplayercoordinates[], wobjectgetphysicscoordinates[wobjectfindnearestbyobjectclass[14]]]<=29}
		DO:	DoAll
				DoExpr {setvar[qcolo-started,1]}
				SetState {MoveToNextRoom}
~~ }
STATE: {MoveToNextRoom} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Moving to Colosseum Room: `+cstr[getvar[qcolo-roomnumber]+1],19]}
	~~ once we enter the portal, don't try to use it anymore
	IF:	IntoPortal
		DO:	SetState {MoveToNextRoomInPortal}
	~~ turn off looting and use the nearest portal.. over and over until it works
	IF:	Always
		DO:	DoAll
				SetOpt {EnableLooting} {false}
				SetOpt {EnableCombat} {false}
				DoExpr {actiontryuseitem[wobjectfindnearestbyobjectclass[14]]}
				SetWatchdog 2 2 {MoveToNextRoom}
~~ }
STATE: {MoveToNextRoomInPortal} ~~ {
	~~ this is only to keep us from doing stuff while we're in the portal
	IF:	ExitPortal
		DO:	SetState {StartCurrentRoom}
	IF: SecsInStateGE 40
		DO:	DoAll
				Chat {/f Stuck In Portal!}
				SetState {GoToColo}
~~ }
STATE: {WaitingForRewards} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Waiting for Rewards Timer to Run Out`,19]}
	~~ if we started colo and we're at this step, then lets see if we're ready for turn in
	IF:	Always
		DO:	Chat {/ub myquests}
	~~ keep checking every 30 seconds
	IF:	SecsInStateGE 30
		DO:	SetState {WaitingForRewards}
	~~ if all our flags are good, we can finally turn in!
	IF:	All
			Not Expr {isrefreshingquests[]}
			Expr {getqueststatus[coloarenaoneaccess]}
			Expr {getqueststatus[coloarenatwoaccess]}
			Expr {getqueststatus[coloarenathreeaccess]}
			Expr {getqueststatus[coloarenafouraccess]}
			Expr {getqueststatus[coloarenafiveaccess]}
		DO:	SetState {SynchronizeRewardTurnIn}
~~ }
STATE: {SynchronizeRewardTurnIn} ~~ {
	IF: Always
		DO:	DoExpr {echo[`[META]: Synchronizing all Characters @ Reward Turn In`,19]}
	IF:	Not Expr {listcontains[getgvar[qcolo-charsreadyforturnin],getcharstringprop[1]]}
		DO:	DoExpr {listadd[getgvar[qcolo-charsreadyforturnin],getcharstringprop[1]]}
	IF:	All
			SecsInStateGE 2
			Not Expr {listcontains[getgvar[qcolo-charsreadyforturnin],getcharstringprop[1]]}
		DO:	SetState {SynchronizeRewardTurnIn}
	IF: Expr {listcount[getgvar[qcolo-charsreadyforturnin]]>=listcount[getgvar[qcolo-chars]]}
		DO:	DoAll
				DoExpr {echo[`[META]: Synchronized `+listcount[getgvar[qcolo-charsreadyforturnin]]+` Characters @ Reward Turn In`,19]}
				SetState {TurnInRewards}
~~ }
STATE: {TurnInRewards} ~~ {
	IF:	Always
		DO:	DoExpr {echo[`[META]: Collecting Rewards from Master Arbitrator`,19]}
	~~ this actually gets us the reward
	IF:	SecsInStateGE 3
		DO:	DoAll
				DoExpr {setgvar[qcolo-charsbuffed,listcreate[]]}
				DoExpr {setgvar[qcolo-charswaiting,listcreate[]]}
				DoExpr {setgvar[qcolo-fellowlocked,0]}
				DoExpr {setvar[qcolo-started,0]}
				EmbedNav navTalkMasterArbitrator {TalkMasterArbitrator}
	~~ once we get our rewards, go figure out what to do
	IF:	ChatMatch {You\'ve earned .* experience\.}
		DO:	SetState {ProgressCheck}
	~~ if we think we're done but we're not, well keep on going.
	IF:	ChatMatch {Master Arbitrator tells you\, \"Your fellowship\'s Arena battles still continue. }
		DO:	SetState {EnterFirstRoom}
	~~ if we thought we did colo, but we actually didnt.. time for a fresh start
	IF:	ChatMatch {Master Arbitrator tells you\, \"If you wish to fight as a gladiator in the Arena}
		DO:	SetState {WaitingForStart}
~~ }
STATE: {LootRings} ~~ {
	IF: Always
		DO:	DoExpr {echo[`[META]: Looting Colo Rings`,19]}
	IF:	ItemCountGE 1 {Colosseum Vault Key}
		DO:	SetState {GoToColosseumVault}
	IF:	ItemCountLE 0 {Colosseum Vault Key}
		DO:	SetState {ApplyIvoryToColoRings}
~~ }
STATE: {GoToColosseumVault} ~~ {
	IF: Always
		DO: DoAll
				SetOpt {EnableCombat} {false}
				SetOpt {EnableLooting} {false}
				Chat {/mt combatstate peace}
	IF: All
			NavEmpty
			Expr {wobjectfindnearestbytemplatetype[34608]&&coordinatedistanceflat[wobjectgetphysicscoordinates[wobjectfindnearestbytemplatetype[34608]],getplayercoordinates[]]<5}
		DO:	SetState {UnlockColosseumVault}
	IF:	Not Expr {wobjectfindnearestbytemplatetype[34608]&&coordinatedistanceflat[wobjectgetphysicscoordinates[wobjectfindnearestbytemplatetype[34608]],getplayercoordinates[]]<5}
		DO: EmbedNav navColosseumVault {ColosseumVault}
~~ }
STATE: {UnlockColosseumVault} ~~ {
	IF: Always
		DO:	DoAll
				Chat {/mt use Colosseum Vault Key on Colosseum Vault}
				SetWatchdog 3 3 {UnlockColosseumVault}
	IF:	ChatMatch {^The Colosseum Vault has been unlocked\.}
		DO:	SetState {OpenColosseumVault}
~~ }
STATE: {OpenColosseumVault} ~~ {
	IF:	SecsInStateGE 1
		DO:	DoAll
				Chat {/mt use Colosseum Vault}
				SetWatchdog 3 3 {UnlockColosseumVault}
	IF: Expr {wobjectgetopencontainer[]&&1}
		DO: SetState {LootEmpyreanRing}
~~ }
STATE: {LootEmpyreanRing} ~~ {
	IF:	All
			SecsInStateGE 2
			Expr {listcount[listfilter[wobjectfindallbycontainer[wobjectgetopencontainer[]],`getregexmatch[wobjectgetname[$1],\`.* Empyrean Ring\`]`]] > 0}
		DO: DoExpr {actiontryuseitem[listgetitem[listfilter[wobjectfindallbycontainer[wobjectgetopencontainer[]],`getregexmatch[wobjectgetname[$1],\`.* Empyrean Ring\`]`],0]]}
	IF:	All
			SecsInStateGE 4
			Not Expr {listcount[listfilter[wobjectfindallbycontainer[wobjectgetopencontainer[]],`getregexmatch[wobjectgetname[$1],\`.* Empyrean Ring\`]`]] > 0}
		DO: DoExpr {wobjectgetopencontainer[]&&actiontryuseitem[wobjectgetopencontainer[]]}
	IF: Expr {wobjectgetopencontainer[]==0}
		DO: SetState {LootRings}
~~ }
STATE: {FixBuggedColosseumVault} ~~ {
	~~IF:	Always
	~~	DO:	DoAll
	~~			DoExpr {wobjectgetopencontainer[]&&actiontryuseitem[wobjectgetopencontainer[]]}
	~~			SetWatchdog 3 3 {FixBuggedColosseumVault}
	IF: Always
		DO:	DoAll
				EmbedNav navResetColosseumVault {ResetColosseumVault}
	IF:	All
			NavEmpty
			Expr {wobjectgetopencontainer[]==0}
		DO: SetState {OpenColosseumVault}
~~ }
STATE: {ApplyIvoryToColoRings} ~~ {
	IF:	All 
			Expr {listcount[listfilter[wobjectfindallinventorybynamerx[`Salvage \\(100\\)`],`wobjectgetintprop[$1,131]==51`]]>0}
			Expr {listcount[listfilter[wobjectfindallinventorybynamerx[`^(?!Enhanced).* Empyrean Ring`],`wobjectgetintprop[$1,114]==1`]]>0}
		DO:	DoAll
				DoExpr {actiontryapplyitem[listgetitem[listfilter[wobjectfindallinventorybynamerx[`Salvage \\(100\\)`],`wobjectgetintprop[$1,131]==51`],0],listgetitem[listfilter[wobjectfindallinventorybynamerx[`^(?!Enhanced).* Empyrean Ring`],`wobjectgetintprop[$1,114]==1`],0]}
				SetState {ApplyIvoryToColoRings}
	~~IF:	Any 
	~~		Expr {listcount[listfilter[wobjectfindallinventorybynamerx[`Salvage \\(100\\)`],`wobjectgetintprop[$1,131]==51`]]<=0}
	~~		Expr {listcount[listfilter[wobjectfindallinventorybynamerx[`^(?!Enhanced).* Empyrean Ring`],`wobjectgetintprop[$1,114]==1`]]<=0}
	~~	DO:	SetState {WaitingForStart}
~~ }
STATE: {BuyTickets} ~~ {
	IF:	ItemCountGE 20 {Colosseum Ticket}
		DO:	SetState {WaitingForStart}
	IF:	ItemCountLE 19 {Colosseum Ticket}
		DO:	DoAll
				Chat {/ub give 1 Trade Note (250,000) to Colosseum Ticket Vendor}
				SetState {BuyTickets}
~~ }
~~========================= ONLY NAVS APPEAR BELOW THIS LINE =========================~~

NAV: navNone once ~~ {
~~ }
NAV: navTalkMasterArbitrator once ~~ {
	pnt -101.783239634832 38.0445641900102 2.08333134651184E-05
	tlk -101.783916680018 37.9945620814959 2.08333134651184E-05 -101.783333333333 38.05 3.12502197630238E-05 37 {Master Arbitrator}
~~ }
NAV: navColosseumVault once ~~ {
	pnt -101.834077048302 38.0082315127055 2.08333134651184E-05
~~ }
NAV: navResetColosseumVault once ~~ {
	pnt -101.800076643626 38.0178716758887 2.08333134651184E-05
	pnt -101.834182055791 38.0088105320931 2.08333134651184E-05
~~ }
